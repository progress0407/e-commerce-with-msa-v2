plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.2' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
//	id 'org.graalvm.buildtools.native' version '0.11.4'
}

allprojects {
	group = 'io.philo'
	version = '0.0.1-SNAPSHOT'
	description = 'A simple MSA-based e-commerce demo project'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	bootJar.enabled = true
	jar.enabled = false

	dependencies {
		// Actuator
		implementation 'org.springframework.boot:spring-boot-starter-actuator'

		// Validate
		implementation 'org.springframework.boot:spring-boot-starter-validation'

		// Lombok
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testCompileOnly 'org.projectlombok:lombok'
		testAnnotationProcessor 'org.projectlombok:lombok'

		// Development Support Tools
		developmentOnly 'org.springframework.boot:spring-boot-devtools'
		annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

		// Test
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	repositories {
		mavenCentral()
	}

	tasks.test {
		useJUnitPlatform()
	}
}

project(':eureka') {
	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
	}
}

project(':api-gateway') {
	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-starter-gateway-server-webflux'
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	}
}

project(':item-service') {
	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		runtimeOnly 'com.h2database:h2'
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	}
}

project(':order-service') {
	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		runtimeOnly 'com.h2database:h2'
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	}
}
